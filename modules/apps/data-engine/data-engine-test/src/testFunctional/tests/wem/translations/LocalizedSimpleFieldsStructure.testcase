@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Web Content Administration";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-122278. As Content Creator, I want to define reusable translations when creating WEM Structures without nested fields or fieldsets"
	@priority = "4"
	test CheckDefaultStructureTitleDisplayedOnStructureList {
		var defaultStructureName = "WC Structure Name EN";
		var defaultStructureName_pt = "WC Structure Name PT";
		var key_urlAppend = "pt/group/test-site-name/~/control_panel/manage?p_p_id=com_liferay_journal_web_portlet_JournalPortlet&p_p_lifecycle=0&p_p_state=maximized&p_p_mode=view&_com_liferay_journal_web_portlet_JournalPortlet_mvcPath=%2Fview_ddm_structures.jsp";

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "${defaultStructureName}");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		PortletEntry.changeLocale(locale = "pt-BR");

		WebContentStructures.addName(structureName = "${defaultStructureName_pt}");

		WebContentStructures.saveCP();

		WebContentStructures.viewCP(structureName = "${defaultStructureName}");

		Navigator.openWithAppendToBaseURL(urlAppend = "${key_urlAppend}");

		AssertTextEquals(
			key_columnName = "name",
			key_tableEntry = "${defaultStructureName_pt}",
			locator1 = "LexiconTable#TABLE_ENTRY_INFO",
			value1 = "${defaultStructureName_pt}");
	}

	@description = "This is a test for LPS-122278. As Content Creator, I want to define reusable translations when creating WEM Structures without nested fields or fieldsets"
	@priority = "4"
	test CheckLanguageSelector {
		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		Click.clickNoMouseOver(locator1 = "Button#LOCALIZATION_GENERIC");

		AssertElementPresent(
			locator1 = "WCD#WEB_CONTENT_TRANSLATION_SPAN_LANGUAGE_ENGLISH");
	}

	@description = "This is a test for LPS-122278. As Content Creator, I want to define reusable translations when creating WEM Structures without nested fields or fieldsets"
	@priority = "4"
	test CheckLocalizationOfFieldsUponLanguageChange {
		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text Test",
			fieldName = "Text");

		PortletEntry.changeLocale(locale = "pt-BR");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Text Test",
			fieldHelp = "Texto teste ajuda");

		WebContentStructures.addName(structureName = "Nome da Estrutura WC");

		FormViewBuilder.editFieldLabel(
			fieldLabel = "Text Test",
			label = "Texto Teste");

		WebContentStructures.saveCP();

		WebContentStructures.viewCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		LexiconEntry.gotoEntry(rowEntry = "WC Structure Name");

		PortletEntry.changeLocale(locale = "pt-BR");

		DataEngine.viewField(
			fieldFieldLabel = "Texto Teste",
			fieldHelp = "Texto teste ajuda",
			fieldName = "Text");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "Nome da Estrutura WC");
	}

	@description = "This is a test for LPS-122278. As Content Creator, I want to define reusable translations when creating WEM Structures without nested fields or fieldsets"
	@priority = "4"
	test DefaultTitleIsDisplayedUponLocaleChange {
		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		PortletEntry.changeLocale(locale = "pt-BR");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "WC Structure Name");
	}

	@description = "This is a test for LPS-122278. As Content Creator, I want to define reusable translations when creating WEM Structures without nested fields or fieldsets"
	@priority = "4"
	test DefaultTitlePersistsAfterTranslatingForAnotherLocale {
		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Text",
			fieldHelp = "Text Help Text");

		PortletEntry.changeLocale(locale = "pt-BR");

		WebContentStructures.addName(structureName = "Nome da Estrutura WC");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Text",
			fieldFieldLabelEdit = "Texto");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Texto",
			fieldHelp = "Texto de ajuda");

		WebContentStructures.saveCP();

		WebContentStructures.viewCP(structureName = "WC Structure Name");

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "WC Structure Name");
	}

	@description = "This is a test for LPS-122278. As Content Creator, I want to define reusable translations when creating WEM Structures without nested fields or fieldsets"
	@priority = "4"
	test DefaultTitlePersistsUponFirstlyTranslatingForAnotherLocale {
		property portal.acceptance = "false";

		LexiconEntry.gotoAdd();

		AssertVisible(locator1 = "Sidebar#FORM_BUILDER");

		DataEngine.addField(
			fieldFieldLabel = "Lbltest",
			fieldName = "Text");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Lbltest",
			fieldHelp = "Help Text test");

		PortletEntry.changeLocale(locale = "pt-BR");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Lbltest",
			fieldHelp = "Texto de ajuda");

		FormViewBuilder.editFieldLabel(
			fieldLabel = "Lbltest",
			label = "Lbltest pt");

		WebContentStructures.addName(structureName = "Nome da Estrutura WC");

		PortletEntry.changeLocale(locale = "en-US");

		WebContentStructures.addName(structureName = "WC Structure Name");

		WebContentStructures.saveCP();

		WebContentStructures.viewCP(structureName = "WC Structure Name");

		LexiconEntry.gotoEntry(rowEntry = "WC Structure Name");
	}

	@description = "This is a test for LPS-122278. As Content Creator, I want to define reusable translations when creating WEM Structures without nested fields or fieldsets"
	@priority = "4"
	test DuplicateTranslatedField {
		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		PortletEntry.changeLocale(locale = "pt-BR");

		WebContentStructures.addName(structureName = "Nome da Estrutura WC");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Text",
			fieldFieldLabelEdit = "Texto");

		DataEngine.duplicate(fieldFieldLabel = "Texto");

		WebContentStructures.saveCP();

		WebContentStructures.viewCP(structureName = "WC Structure Name");

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		PortletEntry.changeLocale(locale = "pt-BR");

		DataEngine.viewField(
			fieldFieldLabel = "Texto",
			fieldName = "Text");
		
		DataEngine.viewField(
			fieldFieldLabel = "Copy of Texto",
			fieldName = "Text");
	}

	@description = "This is a test for LPS-122278. As Content Creator, I want to define reusable translations when creating WEM Structures without nested fields or fieldsets"
	@ignore = "true"
	@priority = "5"
	test TranslateStructureWithAllFields {
		property portal.acceptance = "false";

		// TODO LPS-122278 TranslateStructureWithAllFields pending implementation

	}

	@description = "This is a test for LPS-122278. As Content Creator, I want to define reusable translations when creating WEM Structures without nested fields or fieldsets"
	@priority = "4"
	test TranslateStructureWithoutDefaultTitle {
		LexiconEntry.gotoAdd();

		AssertVisible(locator1 = "Sidebar#FORM_BUILDER");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		PortletEntry.changeLocale(locale = "pt-BR");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Text",
			fieldFieldLabelEdit = "Texto");

		WebContentStructures.addName(structureName = "Nome da Estrutura WC");

		PortletEntry.changeLocale(locale = "en-US");

		Button.clickSave();

		AssertElementPresent(locator1 = "TextInput#REQUIRED_ALERT");

		Alert.viewErrorMessage(errorMessage = "Please enter a valid title for the default language: en-US");
	}

	@description = "This is a test for LPS-122278. As Content Creator, I want to define reusable translations when creating WEM Structures without nested fields or fieldsets"
	@priority = "5"
	test TranslationDoesNotInterfereWithAnother {
		var defaultStructureName = "WC Structure Name";
		var fieldLabelDefault = "Text Test";

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "${defaultStructureName}");

		DataEngine.addField(
			fieldFieldLabel = "${fieldLabelDefault}",
			fieldName = "Text");

		DataEngine.editFieldTip(
			fieldFieldLabel = "${fieldLabelDefault}",
			fieldHelp = "Help Text test");

		PortletEntry.changeLocale(locale = "pt-BR");

		DataEngine.editFieldTip(
			fieldFieldLabel = "${fieldLabelDefault}",
			fieldHelp = "Texto teste ajuda");

		WebContentStructures.addName(structureName = "Nome da Estrutura WC");

		FormViewBuilder.editFieldLabel(
			fieldLabel = "${fieldLabelDefault}",
			label = "Texto Teste");

		PortletEntry.changeLocale(locale = "de-DE");

		DataEngine.editFieldTip(
			fieldFieldLabel = "${fieldLabelDefault}",
			fieldHelp = "Text test help German");

		WebContentStructures.addName(structureName = "${defaultStructureName} German");

		FormViewBuilder.editFieldLabel(
			fieldLabel = "${fieldLabelDefault}",
			label = "Text test German");

		WebContentStructures.saveCP();

		WebContentStructures.viewCP(
			structureDescription = "WC Structure Description",
			structureName = "${defaultStructureName}");

		LexiconEntry.gotoEntry(rowEntry = "${defaultStructureName}");

		PortletEntry.changeLocale(locale = "pt-BR");

		DataEngine.viewField(
			fieldFieldLabel = "Texto Teste",
			fieldHelp = "Texto teste ajuda",
			fieldName = "Text");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Texto Teste",
			fieldHelp = "Texto teste ajuda editado");

		FormViewBuilder.editFieldLabel(
			fieldLabel = "Texto Teste",
			label = "Texto Teste editado");

		WebContentStructures.saveCP();

		WebContentStructures.viewCP(
			structureDescription = "WC Structure Description",
			structureName = "${defaultStructureName}");

		LexiconEntry.gotoEntry(rowEntry = "${defaultStructureName}");

		PortletEntry.changeLocale(locale = "de-DE");

		DataEngine.viewField(
			fieldFieldLabel = "Text test German",
			fieldHelp = "Text test help German",
			fieldName = "Text");
	}

	@description = "This is a test for LPS-122278. As Content Creator, I want to define reusable translations when creating WEM Structures without nested fields or fieldsets"
	@priority = "4"
	test VerifyDefaultLanguageIsFirstShown {
		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text Test",
			fieldName = "Text");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Text Test",
			fieldHelp = "Help Text test");

		PortletEntry.changeLocale(locale = "pt-BR");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Text Test",
			fieldHelp = "Texto teste ajuda");

		WebContentStructures.addName(structureName = "Nome da Estrutura WC");

		FormViewBuilder.editFieldLabel(
			fieldLabel = "Text Test",
			label = "Texto Teste");

		WebContentStructures.saveCP();

		WebContentStructures.viewCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		LexiconEntry.gotoEntry(rowEntry = "WC Structure Name");

		PortletEntry.viewLocale(locale = "en-US");
	}

}